apply plugin: 'com.android.application'

android {
    String OPENCV_DIR = 'libs/3rdparty/OpenCV3.4.1Java'

    String OPENCV_DIR_jni = OPENCV_DIR + '/sdk/native/jni'
    String OPENCV_DIR_nativelibs = OPENCV_DIR + '/sdk/native/libs'

    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion "${rootProject.ext.androidBuildToolsVersion}"

    defaultConfig {
        applicationId "com.tzutalin.dlibtest"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.releaseVersionCode
        versionName "${rootProject.ext.releaseVersionName}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions -g0 -O2"
                arguments "-DOpenCV_DIR=" + OPENCV_DIR_jni
                arguments "-DVERBOSE=1 "
                arguments "-DCMAKE_BUILD_TYPE=Release"
                arguments "-DANDROID_STL=gnustl_shared"
                arguments "-DCMAKE_SHARED_LINKER_FLAGS=-Wl,--exclude-libs,libippicv.a -Wl,--exclude-libs,libippiw.a"
                abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'mips64'
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = [OPENCV_DIR_nativelibs]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}


buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.androidSupportSdkVersion}"
    implementation "com.android.support:design:${rootProject.ext.androidSupportSdkVersion}"
    implementation 'com.github.dexafree:materiallist:3.0.1'
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation project(':dlib')
    // Add AndroidJUnit
    androidTestImplementation "com.android.support:support-annotations:${rootProject.ext.androidSupportSdkVersion}"
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    implementation project(':openCVLibrary341')
}
apply plugin: 'com.jakewharton.hugo'

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a':1, x86:2, 'arm64-v8a':3]

import com.android.build.OutputFile

// For each APK output variant, override versionCode with a combination of
// ext.abiCodes * 1000 + variant.versionCode. In this example, variant.versionCode
// is equal to defaultConfig.versionCode. If you configure product flavors that
// define their own versionCode, variant.versionCode uses that value instead.
android.applicationVariants.all { variant ->

    // Assigns a different version code for each output APK
    // other than the universal APK.
    variant.outputs.each { output ->

        // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
        def baseAbiVersionCode =
                // Determines the ABI for this variant and returns the mapped value.
                project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

        // Because abiCodes.get() returns null for ABIs that are not mapped by ext.abiCodes,
        // the following code does not override the version code for universal APKs.
        // However, because we want universal APKs to have the lowest version code,
        // this outcome is desirable.
        if (baseAbiVersionCode != null) {

            // Assigns the new version code to versionCodeOverride, which changes the version code
            // for only the output APK, not for the variant itself. Skipping this step simply
            // causes Gradle to use the value of variant.versionCode for the APK.
            output.versionCodeOverride =
                    baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}